/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <amxmisc>
#include <ns>

#define PLUGIN "Team Restrict for NS"
#define VERSION "1.0"
#define AUTHOR "Masked Carrot"

new g_TR = 1 //Team Restrict startup mode - off = 0, on =1

public plugin_init() 
{
	if (ns_is_combat())
	{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	register_concmd("amx_ns_tr","cmd_tr",ADMIN_KICK," Off = 0, On =1")
	}
	else
	{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	}
}
public cmd_tr(id,level,cid)
{
	if (!cmd_access(id,level,cid,2))
	{
		return PLUGIN_HANDLED
	}
	else
	{
		new Arg[7]
		read_argv(1,Arg,6)
		if (equal(Arg,"0"))
		{
			g_TR = 0
			console_print(id,"Team Restrict deactivated.")
		}
		else if (equal(Arg,"1"))
		{
			g_TR = 1
			console_print(id,"Team Restriction activated.")
		}
		else if (equal(Arg,"status"))
		{
			if(g_TR) 
			{
				new msg[]="Team Restrict is ON."
				console_print(id,msg)
			}
			else
			{
				new msg[]="Team Restrict is OFF."
				console_print(id,msg)
			}
		}
	}
	return PLUGIN_HANDLED
}
public client_command(id)
{
	/*This function intercepts a client commands.
	 *We block the command to join aliens and ignore all others.*/
	 
	if(g_TR==1&&(!is_user_bot(id))) //If team restrictions are on, and player is not a bot.
	{
		new arg[32]
		read_argv(0,arg,31)
		if (equal(arg,"jointeamtwo")&&(ns_get_class(id)==CLASS_NOTEAM))
		{
			new msg[]="Humans vs. Bots only.^nPlease join marines."
			
			set_hudmessage(255, 255, 255, 0.4, 0.4, 0, 0.0, 15.0, 0.0, 0.0, 1)
			show_hudmessage(id, msg)
			return PLUGIN_HANDLED_MAIN
		}
	}
	return PLUGIN_CONTINUE
	
	
}
public client_changeteam(id,newteam,oldteam)
{
	/*This is called whenever someone changes teams.
	 *We need this for when they jump through a portal instead of pressing F2.
	 *If they join aliens we send them back to readyroom (F4,F4).
	 *
	 *newteam/oldteam values:
	 *
	 *    0 - unknown
	 *    1 - marine 1
	 *    2 - alien 1
	 *    3 - marine 2
	 *    4 - alien 2
	 ********************/
	
	if (g_TR==1&&(!is_user_bot(id)))	//If team restrictions are on, and player is not a bot.
	{
		if (newteam==2||newteam==4)	//If they joined alien give them a message and send 'em back to ready room.
		{
			new msg[]="Humans vs. Bots only.^nPlease join the marines."
			
			set_hudmessage(255, 255, 255, 0.4, 0.4, 0, 0.0, 15.0, 0.0, 0.0, 1)
			show_hudmessage(id, msg)
			engclient_cmd(id,"readyroom")	//F4
			engclient_cmd(id,"readyroom")	//F4
		}
	}
	return PLUGIN_HANDLED_MAIN
}
	
